# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{ modulesPath, pkgs, ... }:

{
  imports = [
    (modulesPath + "/installer/scan/not-detected.nix")
  ];

  hardware.deviceTree = {
    enable = true;
    name = "rockchip/rk3566-wxy-oec-turbo-4g.dtb";
    filter = "*oec*.dtb";
  };

  boot = {
    loader.grub.enable = false;
    loader.generic-extlinux-compatible.enable = true;
    loader.generic-extlinux-compatible.configurationLimit = 2;
    kernelParams = [
      "console=ttyS0,1500000"
      "console=tty0"
    ];
    kernelPackages = pkgs.linuxPackagesFor pkgs.linux-flippy;
  };

  fileSystems =
    let
      subvols = builtins.listToAttrs (
        map
          (x: {
            name = x;
            value = {
              device = "/dev/disk/by-partlabel/disk-main-root";
              fsType = "btrfs";
              options = [
                "compress-force=zstd"
                "nosuid"
                "nodev"
                "subvol=${x}"
              ];
            };
          })
          [
            "/home"
            "/nix"
            "/var"
          ]
      );
    in
    subvols
    // {
      "/" = {
        device = "tmpfs";
        fsType = "tmpfs";
        options = [
          "relatime"
          "mode=755"
          "nosuid"
          "nodev"
        ];
      };

      "/boot" = {
        device = "/dev/disk/by-partlabel/disk-main-boot";
        fsType = "vfat";
        options = [ "umask=0077" ];
      };

      "/storage" = {
        device = "/dev/disk/by-partlabel/disk-main-storage";
        fsType = "btrfs";
        options = [
          "compress-force=zstd"
          "nosuid"
          "nodev"
        ];
      };
    };

  swapDevices = [ ];

}
